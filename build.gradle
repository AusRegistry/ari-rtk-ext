apply plugin: 'java'
apply plugin: 'maven'

sourceCompatibility = 1.6
version = toolkitVersion

ext {
    junitVersion = '4.8.2'
    xercesVersion = '1.4.4'
    regexpVersion = '1.3'
    artifactsDir = "$projectDir/artifacts"
    gradleWrapperVersion = '1.5'
    pagesProjectDir = "$projectDir/../ari-toolkit-gitpages"
    artifactGroupId = 'au.com.ausregistry'
}

sourceSets {
    test {
        java.srcDir('src/test/unit')
    }
}

repositories {
   mavenCentral()
}

dependencies {
    testCompile "junit:junit:$junitVersion"
    compile "xerces:xerces:$xercesVersion"
    compile files('lib/epp-rtk-java.jar')
    runtime "regexp:regexp:$regexpVersion"
}

tasks.withType(Compile) {
    options.encoding = "UTF-8"
}

tasks.withType(Test) {
    jvmArgs '-Dfile.encoding=UTF8'
}

task cleanArtifacts(type: Delete) {
    delete artifactsDir
}

test {
    outputs.upToDateWhen { false }
}

javadoc {
    destinationDir = file("$artifactsDir/javadoc")
}

clean.dependsOn cleanArtifacts

test.dependsOn jar

//Run this task to download the gradle wrapper corresponding to the gradleWrapperVersion number

task wrapper(type: Wrapper) {
    gradleVersion = "$gradleWrapperVersion"
}

task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

task checkVersionNumbersInReadme << {
    def readme = file("readme.md")
    def looksLikeVersionNumber = ~/\d+\.\d+\.\d+/
    def versionNumbersInReadmeFile = readme.readLines().collectMany { line ->
        looksLikeVersionNumber.matcher(line).collect { match ->
            (toolkitVersion == match) ? null : match
        }.findResults{ x -> x }
    }
    assert versionNumbersInReadmeFile.isEmpty()
}

uploadArchives.dependsOn checkVersionNumbersInReadme, javadoc

uploadArchives {
    uploadDescriptor = false
    repositories {
        flatDir {
            dirs artifactsDir
        }

        mavenDeployer {
            repository(url: "file://$pagesProjectDir/repo")
            pom.groupId = artifactGroupId
            pom.artifactId = rootProject.name
            pom.version = toolkitVersion
        }
    }
}

uploadArchives << {
        copy {
            from(javadoc.destinationDir)
            into "$pagesProjectDir/javadoc/ari-rtk-addon"
        }
}